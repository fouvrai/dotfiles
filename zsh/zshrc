
#
# Config
#

# vi mode
# do this first to ensure all subsequent bindings apply to this mode
bindkey -v

# enable advanced glob features
setopt extended_glob

# get all .zsh files in our config path except antigen
config_files=($XDG_CONFIG_HOME/**/*.zsh~*/antigen/*)

# source them all
for file in $config_files; do
    source $file
done

# we no longer need the variable
unset config_files

#
# Local
#

# source a local zshrc for private/untracked stuff
local_zshrc=~/.zshrc
[[ -a $local_zshrc ]] && source $local_zshrc
unset local_zshrc

#
# Plugins
#

export ANTIBODY_HOME=$ZCACHEDIR/antibody
source <(antibody init)

antibody bundle mafredri/zsh-async
antibody bundle jaredru/zsh-polka
antibody bundle jaredru/zsh-pure
antibody bundle zsh-users/zsh-syntax-highlighting

#
# Profile
#

# it'd be nice if there was a way to close around this.
# i don't honestly need it set forever.
profile=$0

profile-edit() {
    $EDITOR $profile
}

profile-reload() {
    source $profile
}

alias profile="profile-edit"
alias reload="profile-reload"

#
# Options
#

# save command history
# save 10k commands total
# save 10k commands per session
HISTFILE=$ZCACHEDIR/history
SAVEHIST=100000
HISTSIZE=100000

# better color switching
# https://wiki.archlinux.org/index.php/zsh#Colors
autoload -Uz colors && colors

# better tab completion support
autoload -Uz compinit && compinit -d $ZCACHEDIR/compdump

# be case insensitive
# i need to read into this more, but the second bit (r:...) matches within filenames as well
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

# use an interactive menu
zstyle ":completion:*" menu select

# complete the first result by default
setopt menu_complete

# <S-TAB> goes backwards
bindkey "\e[Z" reverse-menu-complete

